buildscript {
    ext {
        springBootVersion = '3.3.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'io.spring'
    version '1.0'

    sourceCompatibility = '17'
    targetCompatibility = '17'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation 'org.assertj:assertj-core:3.25.3'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }
}

project(':core:core-api') {
    bootJar {
        enabled = true
        archiveFileName = 'planner-api.jar'
    }
    jar { enabled = true }

    dependencies {
        /*spring web*/
        implementation 'org.springframework.boot:spring-boot-starter-web'

        /*spring data*/
        implementation 'org.springframework.data:spring-data-commons'

        /*spring validation*/
        implementation("org.springframework.boot:spring-boot-starter-validation")

        /*swagger*/
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

        implementation project(":core:core-domain")
        implementation project(":core:core-authorization")
        runtimeOnly project(":storage:db-jpa")

        testRuntimeOnly 'com.h2database:h2'
    }
}

project(':core:core-domain') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.security:spring-security-crypto'
    }
}

project(':core:core-authorization') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'io.jsonwebtoken:jjwt:0.12.5'
    }
}

project(':storage:db-jpa') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        /* jakarta querydsl */
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
        annotationProcessor "jakarta.annotation:jakarta.annotation-api"
        annotationProcessor "jakarta.persistence:jakarta.persistence-api"

        runtimeOnly 'com.h2database:h2'
        testRuntimeOnly 'com.h2database:h2'
        compileOnly project(":core:core-domain")
    }
}
